(*
**  Analog Circuit Design using Grammatical Evolution (ACID-GE)
**  Analog Circuit Design using Multigrammatical Evolution (ACID-MGE)
**
**  Copyright (c) 2021 Federico Castejon, Enrique J. Carmona
**
**  This program is free software: you can redistribute it and/or modify
**  it under the terms of the GNU General Public License as published by
**  the Free Software Foundation, either version 3 of the License, or
**  (at your option) any later version.
**
**  This program is distributed in the hope that it will be useful,
**  but WITHOUT ANY WARRANTY; without even the implied warranty of
**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
**  GNU General Public License for more details.
**
**  You should have received a copy of the GNU General Public License
**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
*)
(*
 * netlistMOSFET.ebnf
 *
 * Gramática de generación de netlist
 *
 * Considera que va a haber una cabecera y un pie, por lo que sólo se centra en componentes ajenos a la parte fija
 *
 * Introduce resistencias y transistores MOSFET PMOS y NMOS
 *
 * Preparado para cirtuito de función gaussiana, limita el nodo 1
 *)

LIST = LIST, LINEA | LINEA;

LINEA = RESISTENCIA, EOL | NMOS, EOL | PMOS, EOL;

RESISTENCIA =  "R", SEP, nodo, SEP, nodo, SEP, ValorResistencia; 

NMOS = "M", SEP, nodo, SEP, nodo, SEP, nodo, SEP, '5', SEP, MODELNMOS, AnchoCanalMos;

PMOS = "M", SEP, nodo, SEP, nodo, SEP, nodo, SEP, '4', SEP, MODELPMOS, AnchoCanalMos;

MODELNMOS = "NMOS1 L=10u W=";

MODELPMOS = "PMOS1 L=10u W=";

(* Pendiente de ajustar nodos para el circuito de función gaussiana *)
nodo = '0' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' | '10' | '11' ;
	
ValorResistencia = digitonocero, DECIMAL, digito, EXP, digito; 

AnchoCanalMos = digitonocero, digito, 'u' | '1', digito, digito, 'u';

digito = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9';

digitonocero = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9';

SEP = ' ';

EXP = 'e';

DECIMAL = '.';

EOL = ?EOL?;

CR = ?Carriage return?;

